' Pictionary - Append
' Version 1.7.1
' Copyright Â© 2020 Nonki Takahashi.  The MIT License.
' Last update 2020-05-18

Init()
SB_Workaround()
' initialize small shapes
Shapes_Init_Small()
name = "Small"
Sprite_Add()
' initialize basic shapes
Shapes_Init_Basic()
xB = shX
yB = shY
shX = shX + 80
name = "Basic"
Sprite_Add()
iB = nSprite
' initialize caption shapes
Shapes_Init_Caption()
name = "Caption"
Sprite_Add()
i = iB
y = yB
For x = xB + 75 To xB Step -5
  Program.Delay(300)
  Sprite_Move()
EndFor

Sub Init
  GraphicsWindow.Title = "Pictionary"
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  angle = 0
EndSub

Sub SB_RotateWorkaround
  ' Small Basic | rotate workaround for Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param _alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(_alpha) - y1 * Math.Sin(_alpha) + ox
  y = x1 * Math.Sin(_alpha) + y1 * Math.Cos(_alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | workaround for Silverlight / SBD
  ' return silverlight - "True" if in remote
  ' return sbd - "True" if Small Basic Desktop
  color = GraphicsWindow.GetPixel(0, 0)
  sbd = "False"
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
    _gw = GraphicsWindow.Width
    _gh = GraphicsWindow.Height
    If (_gw = 624) And (_gh = 441) Then
      sbd = "True"
    EndIf
  EndIf
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | calculate total width and height of shapes
  ' param shape[] - shape array
  ' return shWidth, shHeight - total size of shapes
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Init_Basic
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 79  ' x offset
  shY = 174 ' y offset
  shape = ""
  shape[1] = "func=rect;x=190;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[2] = "func=text;x=198;y=0;text= ;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[3] = "func=rect;x=228;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[4] = "func=text;x=236;y=0;text=B;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[5] = "func=rect;x=266;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[6] = "func=text;x=274;y=0;text=a;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[7] = "func=rect;x=304;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[8] = "func=text;x=312;y=0;text=s;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[9] = "func=rect;x=342;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[10] = "func=text;x=350;y=0;text=i;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[11] = "func=rect;x=380;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[12] = "func=text;x=388;y=0;text=c;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
EndSub

Sub Shapes_Init_Caption
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 401 ' x offset
  shY = 358 ' y offset
  shape = ""
  shape[1] = "func=text;x=0;y=0;text=Append;fn=Trebuchet MS;fs=50;fb=True;pw=0;bc=Black;"
EndSub

Sub Shapes_Init_Small
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 79  ' x offset
  shY = 174 ' y offset
  shape = ""
  shape[1] = "func=rect;x=0;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[2] = "func=text;x=8;y=0;text=S;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[3] = "func=rect;x=38;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[4] = "func=text;x=46;y=0;text=m;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[5] = "func=rect;x=76;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[6] = "func=text;x=84;y=0;text=a;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[7] = "func=rect;x=114;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[8] = "func=text;x=122;y=0;text=l;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
  shape[9] = "func=rect;x=152;y=0;width=40;height=40;pw=2;pc=DarkCyan;bc=White;"
  shape[10] = "func=text;x=160;y=0;text=l;fn=Courier New;fs=40;fb=False;fi=False;pw=2;pc=DarkCyan;bc=Black;"
EndSub

Sub Sprite_Add
  ' Sprite | add shapes to a sprite
  ' param name - sprite name
  ' param shX, shY - origin of shape array
  ' param scale - to resize 
  ' param shape[] - shape array
  ' param nSprite - number of sprite
  ' return nSprite - updated number of sprite
  ' return sprite[] - sprite array
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  nSprite = nSprite + 1
  spr = ""
  spr["name"] = name
  spr["x"] = shX
  spr["y"] = shY
  spr["angle"] = 0
  spr["dir"] = 1
  Shapes_CalcWidthAndHeight()
  spr["width"] = shWidth
  spr["height"] = shHeight
  spr["cx"] = shWidth / 2
  spr["cy"] = shHeight / 2
  If scale = "" Then
    scale = 1
  EndIf
  s = scale
  spr["scale"] = s
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text|btn", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If Text.IsSubText("text|btn", shp["func"]) Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      If shp["fb"] = "False" Then
        GraphicsWindow.FontBold = "False"
      Else
        GraphicsWindow.FontBold = "True"
      EndIf
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If sbd And (shp["func"] = "line") Then
      shp["wx"] = x
      shp["wy"] = y
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    If shp["func"] = "btn" Then
      shp["obj"] = Controls.AddButton(shp["caption"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  spr["shape"] = shape
  sprite[nSprite] = spr
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub

Sub Sprite_Move
  ' Sprite | move a sprite
  ' param sprite[i] - sprite to move
  ' param x, y - position to move
  ' return sprite[i] - updated sprite
  Stack.PushValue("local", j)
  spr = sprite[i]
  s = spr["scale"]
  spr["x"] = x
  spr["y"] = y
  shape = spr["shape"]
  n = Array.GetItemCount(shape)
  For j = 1 To n
    shp = shape[j]
    If sbd And (shp["func"] = "line") Then
      _x = shp["wx"]
      _y = shp["wy"]
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _x = shp["wx"]
      _y = shp["wy"]
    Else
      _x = shp["rx"]
      _y = shp["ry"]
    EndIf
    Shapes.Move(shp["obj"], spr["x"] + _x * s, spr["y"]  + _y * s)
  EndFor
  sprite[i] = spr
  j = Stack.PopValue("local")
EndSub
