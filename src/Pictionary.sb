' Pictionary
' Version 1.0.0
' Copyright Â© 2017-2020 Nonki Takahashi.  The MIT License.
' Last update 2020-04-17
' Using Extension LitDev

title = "Pictionary 1.0.0"
GraphicsWindow.Title = title
GraphicsWindow.BackgroundColor = "AntiqueWhite"
kw = "And,Array,Bell,Box,Brush,"
kw = kw + "Caption,Ceiling,Chime,Chinese,"
kw = kw + "Click,Clock,Color,Control,Cosine,"
kw = kw + "Date,Degree,Desktop,Dictionary,Draw,"
kw = kw + "Ellipse,English,Error,"
kw = kw + "File,Fill,Flickr,Floor,Font,French,"
kw = kw + "German,Go,Graphics,Height,Hour,"
kw = kw + "Image,Italian,Japanese,"
kw = kw + "Korean,Length,Line,Math,Minute,Mouse,Move,Music,"
kw = kw + "Network,Or,Paper,Pen,Program,"
kw = kw + "Radian,Random,Rectangle,Resize,RGB,Ring,Rotate,"
kw = kw + "Second,Shape,Shell,Sine,Sound,Spanish,"
kw = kw + "Square,Stack,Stop,Subroutine,"
kw = kw + "Tangent,Text,Timer,Triangle,Turn,Turtle,"
kw = kw + "Wall,Web,Width,Window,Write,Zoom"
keywords = LDText.Split(kw, ",")
n = Array.GetItemCount(keywords)
GraphicsWindow.Title = title + " - " + n + " keywords"
LDSpeech.Voice = "Microsoft Zira Desktop"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontName = "Trebuchet MS"
xLeft = 10
y[0] = -20
dx = 80
For i = 1 To n
  y[i] = y[i - 1] + 30
  If 400 < y[i] Then
    xLeft = xLeft + dx
    y[i] = 10
  EndIf
  x[i] = xLeft
  GraphicsWindow.DrawText(x[i], y[i], keywords[i])
EndFor
GraphicsWindow.MouseDown = OnMouseDown
LDSpeech.Speed = -1
While "True"
  If mouseDown Then
    For i = 1 To n
      If x[i] <= mx And mx <= x[i] + dx And y[i] <= my And my <= y[i] + 12 Then
        LDProcess.Start(Program.Directory + "\" + keywords[i] + ".exe", "")
        LDSpeech.Speak(keywords[i])
        If keywords[i] = "Chime" Then
          Sound.PlayChime()
        ElseIf keywords[i] = "Click" Then
          Sound.PlayClick()
        ElseIf keywords[i] = "Music" Then
          Sound.PlayMusic("O5C8D8E8F8G8A8B8O6C4O5G4O6C4")
        ElseIf keywords[i] = "Ring" Then
          Sound.PlayBellRing()
        EndIf
      EndIf
    EndFor
    mouseDown = "False"
  EndIf
  Program.Delay(200)
EndWhile

Sub OnMouseDown
  mouseDown = "True"
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
EndSub
