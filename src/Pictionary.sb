' Pictionary
' Version 1.6.0
' Copyright Â© 2017-2020 Nonki Takahashi.  The MIT License.
' Last update 2020-05-15
' Using Extension LitDev

title = "Pictionary 1.6.0"
GraphicsWindow.Title = title
Not = "False=True;True=False;"
gw = 660
gh = 428
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
path = Program.Directory + "\PictionaryIcon.png"
tmp = File.GetTemporaryFilePath()
If Not[LDFile.Exists(path)] Then
  url = "https://raw.githubusercontent.com/nonkitMac/Pictionary/master/img/PictionaryIcon.png"
  LDNetwork.DownloadFile(path, url)
EndIf
LDGraphicsWindow.Icon = path
img = ImageList.LoadImage(path)
iw = ImageList.GetWidthOfImage(img)
ih = ImageList.GetHeightOfImage(img)
icon = Shapes.AddImage(img)
Shapes.Move(icon, gw - iw - 10, gh - ih - 10)
Shapes.SetOpacity(icon, 90)
GraphicsWindow.BackgroundColor = "AntiqueWhite"
kw = "And,Angle,Array,Bell,Bold,Box,Brush,"
kw = kw + "Caption,Case,Ceiling,Chime,Chinese,Click,Clock,"
kw = kw + "Color,Control,Copy,Cosine,Count,Cursor,"
kw = kw + "Date,Degree,Delete,Desktop,Dictionary,Directory,"
kw = kw + "Down,Draw,"
kw = kw + "Ellipse,End,English,Error,"
kw = kw + "File,Fill,Flickr,Floor,Font,For,French,From,"
kw = kw + "German,Go,Graphics,Gray,Height,Hour,"
kw = kw + "Image,Italian,Italic,Japanese,Key,Korean,"
kw = kw + "Left,Length,Line,Lower,"
kw = kw + "Math,Message,Minute,Mouse,Move,Music,Network,"
kw = kw + "Or,Paper,Pen,Pi,Pop,Program,Push,"
kw = kw + "Radian,Random,Rectangle,Remainder,Resize,"
kw = kw + "RGB,Right,Ring,Root,Rotate,"
kw = kw + "Second,Shape,Shell,Simplified,Sine,Size,Sound,"
kw = kw + "Spanish,Square,Stack,Start,Step,Stop,Subroutine,"
kw = kw + "Tangent,Text,Timer,To,Traditional,"
kw = kw + "Triangle,Turn,Turtle,Type,Up,Upper,"
kw = kw + "Wall,Web,Week,Width,Window,Write,Xor,Zoom"
keywords = LDText.Split(kw, ",")
n = Array.GetItemCount(keywords)
GraphicsWindow.Title = title + " - " + n + " keywords"
LDSpeech.Voice = "Microsoft Zira Desktop"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontName = "Trebuchet MS"
xLeft = 10
dx = 80
dy = 26
y[0] = 10 - dy
For i = 1 To n
  y[i] = y[i - 1] + dy
  If 400 < y[i] Then
    xLeft = xLeft + dx
    y[i] = 10
  EndIf
  x[i] = xLeft
  GraphicsWindow.DrawText(x[i], y[i], keywords[i])
EndFor
GraphicsWindow.MouseDown = OnMouseDown
LDSpeech.Speed = -1
While "True"
  If mouseDown Then
    For i = 1 To n
      If x[i] <= mx And mx <= x[i] + dx And y[i] <= my And my <= y[i] + 12 Then
        LDProcess.Start(Program.Directory + "\" + keywords[i] + ".exe", "")
        LDSpeech.Speak(keywords[i])
        If keywords[i] = "Chime" Then
          Sound.PlayChime()
        ElseIf keywords[i] = "Click" Then
          Sound.PlayClick()
        ElseIf keywords[i] = "Music" Then
          Sound.PlayMusic("O5C8D8E8F8G8A8B8O6C4O5G4O6C4")
        ElseIf keywords[i] = "Ring" Then
          Sound.PlayBellRing()
        EndIf
      EndIf
    EndFor
    mouseDown = "False"
  EndIf
  Program.Delay(200)
EndWhile

Sub OnMouseDown
  mouseDown = "True"
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
EndSub
  