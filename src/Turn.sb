' Pictionary - Turn
' Version 1.7.1
' Copyright © 2020 Nonki Takahashi.  The MIT License.
' Last update 2020-05-18

SB_Workaround()
Init()

' initialize trail shapes
Shapes_Init_Trail()
scale = 1
name = "Trail"
Sprite_Add()

' initialize turtle shapes
Shapes_Init_Turtle()
scale = 1.5
name = "Turtle"
Sprite_Add()
i = nSprite
spr = sprite[i]

' initialize caption shapes
Shapes_Init_Caption()
scale = 1
name = "Caption"
Sprite_Add()

' turn turtle
name = "Turtle"
Sprite_GetIndexOf()
cx = 299
cy = 245
rArrow = 210
colorArrow = "#CC0000"
GraphicsWindow.BrushColor = colorArrow
arrow = Shapes.AddTriangle(0, 0, 40, 10, 0, 20)
x1 = cx
y1 = cy - rArrow
Shapes.Move(arrow, x1 - 20, y1 - 10)
GraphicsWindow.PenColor = colorArrow
GraphicsWindow.PenWidth = 4
Program.Delay(500)
For angle = 5 To 60 Step 5
  Sprite_Rotate()
  _angle = Math.GetRadians(angle)
  x2 = cx + rArrow * Math.Sin(_angle)
  y2 = cy - rArrow * Math.Cos(_angle)
  GraphicsWindow.DrawLine(x1, y1, x2, y2)
  Shapes.Move(arrow, x2 - 20, y2 - 10)
  Shapes.Rotate(arrow, angle)
  x1 = x2
  y1 = y2
  Program.Delay(100)
EndFor

Sub Init
  GraphicsWindow.Title = "Pictionary"
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
EndSub

Sub Math_CartesianToPolar
  ' Math | convert Cartesian coodinate to polar coordinate
  ' param x, y - Cartesian coordinate
  ' return r, a - polar Coordinate (0<=a<360)
  r = Math.SquareRoot(x * x + y * y)
  If x = 0 And y > 0 Then
    a = 90 ' [degree]
  ElseIf x = 0 And y < 0 Then
    a = -90
  ElseIf x = 0 And y = 0 Then
    a = 0
  Else
    a = Math.ArcTan(y / x) * 180 / Math.Pi
  EndIf
  ' at this point -90<=a<=90
  If x < 0 Then
    a = a + 180
  ElseIf x >= 0 And y < 0 Then
    a = a + 360
  EndIf
  ' at this point 0<=a<360
EndSub

Sub SB_LineWorkaround
  ' Small Basic | line rotate workaround for SBD
  ' param x, y - coordinate of the position of the line
  ' param x1, y1 - coordinate of the first point
  ' param x2, y2 - coordinate of the second point
  ' param pw - pen width
  ' param alpha - to rotate [degree]
  ' return x, y - workaround value for the coordinate
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  x = x1 - x2
  y = y1 - y2
  Math_CartesianToPolar()
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  _a = Math.GetRadians(a)
  _alpha = Math.GetRadians(a - alpha)
  Δx = pw / 4 * (Math.Sin(_alpha) - Math.Sin(_a))
  Δy = pw / 4 * (Math.Cos(_alpha) - Math.Cos(_a))
  x = x - Δx
  y = y - Δy
EndSub

Sub SB_RotateWorkaround
  ' Small Basic | rotate workaround for Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param _alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(_alpha) - y1 * Math.Sin(_alpha) + ox
  y = x1 * Math.Sin(_alpha) + y1 * Math.Cos(_alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | workaround for Silverlight / SBD
  ' return silverlight - "True" if in remote
  ' return sbd - "True" if Small Basic Desktop
  color = GraphicsWindow.GetPixel(0, 0)
  sbd = "False"
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
    _gw = GraphicsWindow.Width
    _gh = GraphicsWindow.Height
    If (_gw = 624) And (_gh = 441) Then
      sbd = "True"
    EndIf
  EndIf
EndSub

Sub Shapes_CalcRotateZoomPos
  ' Shapes | calculate position for rotated and zoomed shape
  ' param["x"], param["y"] - position of a shape
  ' param["width"], param["height"] - size of a shape
  ' param ["cx"], param["cy"] - center of rotation
  ' param ["angle"] - rotate angle
  ' param ["scale"] - zoom scale
  ' return x, y - rotated position of a shape
  _cx = param["x"] + param["width"] / 2
  _cy = param["y"] + param["height"] / 2
  x = _cx - param["cx"]
  y = _cy - param["cy"]
  Math_CartesianToPolar()
  a = a + param["angle"]
  x = r * Math.Cos(a * Math.Pi / 180) * param["scale"]
  y = r * Math.Sin(a * Math.Pi / 180) * param["scale"]
  _cx = x + param["cx"]
  _cy = y + param["cy"]
  x = _cx - param["width"] / 2
  y = _cy - param["height"] / 2
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | calculate total width and height of shapes
  ' param shape[] - shape array
  ' return shWidth, shHeight - total size of shapes
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Init_Caption
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 471 ' x offset
  shY = 358 ' y offset
  shape = ""
  shape[1] = "func=text;x=0;y=0;text=Turn;fn=Trebuchet MS;fs=50;fb=True;pw=0;bc=Black;"
EndSub

Sub Shapes_Init_Turtle
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 202 ' x offset
  shY = 90  ' y offset
  shape = ""
  shape[1] = "func=ell;x=71;y=127;width=67;height=29;pw=6;pc=#000000;bc=#999900;name=ellipse 8;"
  shape[2] = "func=ell;x=70;y=60;width=67;height=29;pw=6;pc=#000000;bc=#999900;name=ellipse 9;"
  shape[3] = "func=ell;x=0;y=57;width=67;height=29;pw=6;pc=#000000;bc=#999900;name=ellipse 10;"
  shape[4] = "func=ell;x=2;y=127;width=67;height=29;pw=6;pc=#000000;bc=#999900;name=ellipse 11;"
  shape[5] = "func=ell;x=54;y=131;width=22;height=43;pw=6;pc=#000000;bc=#999900;name=ellipse 12;"
  shape[6] = "func=ell;x=27;y=0;width=82;height=60;pw=6;pc=#000000;bc=#339900;name=ellipse 4;"
  shape[7] = "func=ell;x=12;y=47;width=110;height=111;pw=6;pc=#000000;bc=#335323;name=ellipse 5;"
  shape[8] = "func=rect;x=55.75;y=82;width=23.50;height=43;angle=90;pw=0;bc=#AFEC00;name=hexagon 6;"
  shape[9] = "func=tri;x=45;y=84;width=43;height=8;x1=22;y1=0;x2=0;y2=8;x3=43;y3=8;pw=0;bc=#AFEC00;name=hexagon 6;"
  shape[10] = "func=tri;x=44;y=113;width=43;height=8;x1=22;y1=0;x2=0;y2=8;x3=43;y3=8;angle=180;pw=0;bc=#AFEC00;name=hexagon 6;"
  shape[11] = "func=rect;x=66.46;y=85.5;width=23.09;height=6;angle=17.65;pw=0;bc=#338F05;name=hexagon 6;"
  shape[12] = "func=rect;x=77.49;y=100;width=22.02;height=6;angle=92.6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[13] = "func=rect;x=66.26;y=115;width=22.47;height=6;angle=159.15;pw=0;bc=#338F05;name=hexagon 6;"
  shape[14] = "func=rect;x=44.3;y=115;width=23.41;height=6;angle=199.98;pw=0;bc=#338F05;name=hexagon 6;"
  shape[15] = "func=rect;x=34.49;y=100;width=22.02;height=6;angle=272.6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[16] = "func=rect;x=45.43;y=85.5;width=22.14;height=6;angle=341.57;pw=0;bc=#338F05;name=hexagon 6;"
  shape[17] = "func=ell;x=64;y=82;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[18] = "func=ell;x=86;y=89;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[19] = "func=ell;x=85;y=111;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[20] = "func=ell;x=64;y=119;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[21] = "func=ell;x=42;y=111;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[22] = "func=ell;x=43;y=89;width=6;height=6;pw=0;bc=#338F05;name=hexagon 6;"
  shape[23] = "func=rect;x=79.75;y=57;width=15.50;height=30;angle=90;pw=0;bc=#AFEC00;name=hexagon 13;"
  shape[24] = "func=tri;x=71;y=60;width=30;height=4;x1=16;y1=0;x2=0;y2=4;x3=30;y3=4;pw=0;bc=#AFEC00;name=hexagon 13;"
  shape[25] = "func=tri;x=71;y=77;width=30;height=4;x1=16;y1=0;x2=0;y2=4;x3=30;y3=4;angle=180;pw=0;bc=#AFEC00;name=hexagon 13;"
  shape[26] = "func=rect;x=86.74;y=60;width=15.52;height=6;angle=14.93;pw=0;bc=#338F05;name=hexagon 13;"
  shape[27] = "func=rect;x=95.5;y=68.5;width=13;height=6;angle=90;pw=0;bc=#338F05;name=hexagon 13;"
  shape[28] = "func=rect;x=86.74;y=77;width=15.52;height=6;angle=165.07;pw=0;bc=#338F05;name=hexagon 13;"
  shape[29] = "func=rect;x=71.74;y=77;width=15.52;height=6;angle=194.93;pw=0;bc=#338F05;name=hexagon 13;"
  shape[30] = "func=rect;x=65.5;y=68.5;width=13;height=6;angle=270;pw=0;bc=#338F05;name=hexagon 13;"
  shape[31] = "func=rect;x=71.74;y=60;width=15.52;height=6;angle=345.07;pw=0;bc=#338F05;name=hexagon 13;"
  shape[32] = "func=ell;x=84;y=58;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[33] = "func=ell;x=99;y=62;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[34] = "func=ell;x=99;y=75;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[35] = "func=ell;x=84;y=79;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[36] = "func=ell;x=69;y=75;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[37] = "func=ell;x=69;y=62;width=6;height=6;pw=0;bc=#338F05;name=hexagon 13;"
  shape[38] = "func=rect;x=37.75;y=59;width=15.50;height=27;angle=90;pw=0;bc=#AFEC00;name=hexagon 14;"
  shape[39] = "func=tri;x=31;y=61;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;pw=0;bc=#AFEC00;name=hexagon 14;"
  shape[40] = "func=tri;x=30;y=78;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;angle=180;pw=0;bc=#AFEC00;name=hexagon 14;"
  shape[41] = "func=rect;x=44.84;y=60.5;width=14.32;height=6;angle=12.09;pw=0;bc=#338F05;name=hexagon 14;"
  shape[42] = "func=rect;x=52;y=69;width=14;height=6;angle=90;pw=0;bc=#338F05;name=hexagon 14;"
  shape[43] = "func=rect;x=44.72;y=78;width=14.56;height=6;angle=164.05;pw=0;bc=#338F05;name=hexagon 14;"
  shape[44] = "func=rect;x=30.72;y=78;width=14.56;height=6;angle=195.95;pw=0;bc=#338F05;name=hexagon 14;"
  shape[45] = "func=rect;x=24.48;y=69;width=14.04;height=6;angle=274.09;pw=0;bc=#338F05;name=hexagon 14;"
  shape[46] = "func=rect;x=31.83;y=60.5;width=13.34;height=6;angle=347.01;pw=0;bc=#338F05;name=hexagon 14;"
  shape[47] = "func=ell;x=42;y=59;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[48] = "func=ell;x=56;y=62;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[49] = "func=ell;x=56;y=76;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[50] = "func=ell;x=42;y=80;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[51] = "func=ell;x=28;y=76;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[52] = "func=ell;x=29;y=62;width=6;height=6;pw=0;bc=#338F05;name=hexagon 14;"
  shape[53] = "func=rect;x=38.25;y=120;width=14.50;height=27;angle=90;pw=0;bc=#AFEC00;name=hexagon 15;"
  shape[54] = "func=tri;x=31;y=123;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;pw=0;bc=#AFEC00;name=hexagon 15;"
  shape[55] = "func=tri;x=30;y=138;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;angle=180;pw=0;bc=#AFEC00;name=hexagon 15;"
  shape[56] = "func=rect;x=44.84;y=122.5;width=14.32;height=6;angle=12.09;pw=0;bc=#338F05;name=hexagon 15;"
  shape[57] = "func=rect;x=53;y=130;width=12;height=6;angle=90;pw=0;bc=#338F05;name=hexagon 15;"
  shape[58] = "func=rect;x=44.72;y=138;width=14.56;height=6;angle=164.05;pw=0;bc=#338F05;name=hexagon 15;"
  shape[59] = "func=rect;x=30.72;y=138;width=14.56;height=6;angle=195.95;pw=0;bc=#338F05;name=hexagon 15;"
  shape[60] = "func=rect;x=25.48;y=130;width=12.04;height=6;angle=274.76;pw=0;bc=#338F05;name=hexagon 15;"
  shape[61] = "func=rect;x=31.83;y=122.5;width=13.34;height=6;angle=347.01;pw=0;bc=#338F05;name=hexagon 15;"
  shape[62] = "func=ell;x=42;y=121;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[63] = "func=ell;x=56;y=124;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[64] = "func=ell;x=56;y=136;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[65] = "func=ell;x=42;y=140;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[66] = "func=ell;x=28;y=136;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[67] = "func=ell;x=29;y=124;width=6;height=6;pw=0;bc=#338F05;name=hexagon 15;"
  shape[68] = "func=rect;x=79.25;y=121;width=14.50;height=27;angle=90;pw=0;bc=#AFEC00;name=hexagon 16;"
  shape[69] = "func=tri;x=72;y=124;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;pw=0;bc=#AFEC00;name=hexagon 16;"
  shape[70] = "func=tri;x=71;y=139;width=27;height=4;x1=14;y1=0;x2=0;y2=4;x3=27;y3=4;angle=180;pw=0;bc=#AFEC00;name=hexagon 16;"
  shape[71] = "func=rect;x=85.84;y=123.5;width=14.32;height=6;angle=12.09;pw=0;bc=#338F05;name=hexagon 16;"
  shape[72] = "func=rect;x=93.48;y=131;width=12.04;height=6;angle=94.76;pw=0;bc=#338F05;name=hexagon 16;"
  shape[73] = "func=rect;x=85.7;y=139;width=13.6;height=6;angle=162.9;pw=0;bc=#338F05;name=hexagon 16;"
  shape[74] = "func=rect;x=72.7;y=139;width=13.6;height=6;angle=197.1;pw=0;bc=#338F05;name=hexagon 16;"
  shape[75] = "func=rect;x=67;y=131;width=12;height=6;angle=270;pw=0;bc=#338F05;name=hexagon 16;"
  shape[76] = "func=rect;x=72.83;y=123.5;width=13.34;height=6;angle=347.01;pw=0;bc=#338F05;name=hexagon 16;"
  shape[77] = "func=ell;x=83;y=122;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[78] = "func=ell;x=97;y=125;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[79] = "func=ell;x=96;y=137;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[80] = "func=ell;x=83;y=141;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[81] = "func=ell;x=70;y=137;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[82] = "func=ell;x=70;y=125;width=6;height=6;pw=0;bc=#338F05;name=hexagon 16;"
  shape[83] = "func=rect;x=96.00;y=98;width=18.00;height=12;angle=90;pw=0;bc=#AFEC00;name=hexagon 17;"
  shape[84] = "func=tri;x=98;y=90;width=12;height=6;x1=7;y1=0;x2=0;y2=5;x3=12;y3=6;pw=0;bc=#AFEC00;name=hexagon 17;"
  shape[85] = "func=tri;x=97;y=111;width=12;height=6;x1=7;y1=0;x2=0;y2=5;x3=12;y3=6;angle=180;pw=0;bc=#AFEC00;name=hexagon 17;"
  shape[86] = "func=rect;x=104.09;y=90.5;width=7.81;height=6;angle=39.81;pw=0;bc=#338F05;name=hexagon 17;"
  shape[87] = "func=rect;x=102.48;y=101;width=16.03;height=6;angle=93.58;pw=0;bc=#338F05;name=hexagon 17;"
  shape[88] = "func=rect;x=103.09;y=111.5;width=7.81;height=6;angle=140.19;pw=0;bc=#338F05;name=hexagon 17;"
  shape[89] = "func=rect;x=97.09;y=111.5;width=7.81;height=6;angle=219.81;pw=0;bc=#338F05;name=hexagon 17;"
  shape[90] = "func=rect;x=89.99;y=100.5;width=17.03;height=6;angle=273.37;pw=0;bc=#338F05;name=hexagon 17;"
  shape[91] = "func=rect;x=98.39;y=90;width=7.21;height=6;angle=326.31;pw=0;bc=#338F05;name=hexagon 17;"
  shape[92] = "func=ell;x=102;y=88;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[93] = "func=ell;x=108;y=93;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[94] = "func=ell;x=107;y=109;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[95] = "func=ell;x=101;y=114;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[96] = "func=ell;x=95;y=109;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[97] = "func=ell;x=96;y=92;width=6;height=6;pw=0;bc=#338F05;name=hexagon 17;"
  shape[98] = "func=rect;x=20.00;y=97;width=18.00;height=14;angle=90;pw=0;bc=#AFEC00;name=hexagon 18;"
  shape[99] = "func=tri;x=21;y=90;width=14;height=6;x1=8;y1=0;x2=0;y2=5;x3=14;y3=6;pw=0;bc=#AFEC00;name=hexagon 18;"
  shape[100] = "func=tri;x=20;y=111;width=14;height=6;x1=8;y1=0;x2=0;y2=5;x3=14;y3=6;angle=180;pw=0;bc=#AFEC00;name=hexagon 18;"
  shape[101] = "func=rect;x=28.2;y=90.5;width=8.6;height=6;angle=35.54;pw=0;bc=#338F05;name=hexagon 18;"
  shape[102] = "func=rect;x=27.48;y=101;width=16.03;height=6;angle=93.58;pw=0;bc=#338F05;name=hexagon 18;"
  shape[103] = "func=rect;x=27.2;y=111.5;width=8.6;height=6;angle=144.46;pw=0;bc=#338F05;name=hexagon 18;"
  shape[104] = "func=rect;x=20.2;y=111.5;width=8.6;height=6;angle=215.54;pw=0;bc=#338F05;name=hexagon 18;"
  shape[105] = "func=rect;x=12.99;y=100.5;width=17.03;height=6;angle=273.37;pw=0;bc=#338F05;name=hexagon 18;"
  shape[106] = "func=rect;x=21.47;y=90;width=8.06;height=6;angle=330.26;pw=0;bc=#338F05;name=hexagon 18;"
  shape[107] = "func=ell;x=26;y=88;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[108] = "func=ell;x=33;y=93;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[109] = "func=ell;x=32;y=109;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[110] = "func=ell;x=25;y=114;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[111] = "func=ell;x=18;y=109;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[112] = "func=ell;x=19;y=92;width=6;height=6;pw=0;bc=#338F05;name=hexagon 18;"
  shape[113] = "func=ell;x=83;y=22;width=12;height=11;angle=90;pw=0;pc=#FFFFFF;bc=#000000;name=ellipse 19;"
  shape[114] = "func=ell;x=47;y=22;width=12;height=11;angle=90;pw=0;pc=#FFFFFF;bc=#000000;name=ellipse 20;"
EndSub

Sub Shapes_Init_Trail
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 299 ' x offset
  shY = 245 ' y offset
  shape = ""
  shape[1] = "func=line;x1=0;y1=0;x2=0;y2=200;pw=4;pc=Gray;"
EndSub  

Sub Sprite_Add
  ' Sprite | add shapes to a sprite
  ' param name - sprite name
  ' param shX, shY - origin of shape array
  ' param scale - to resize 
  ' param shape[] - shape array
  ' param nSprite - number of sprite
  ' return nSprite - updated number of sprite
  ' return sprite[] - sprite array
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  nSprite = nSprite + 1
  spr = ""
  spr["name"] = name
  spr["x"] = shX
  spr["y"] = shY
  spr["angle"] = 0
  spr["dir"] = 1
  Shapes_CalcWidthAndHeight()
  s = scale
  spr["scale"] = s
  spr["width"] = shWidth
  spr["height"] = shHeight
  spr["cx"] = shWidth / 2
  spr["cy"] = shHeight / 2
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text|btn", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If Text.IsSubText("text|btn", shp["func"]) Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      If shp["fb"] = "False" Then
        GraphicsWindow.FontBold = "False"
      Else
        GraphicsWindow.FontBold = "True"
      EndIf
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If sbd And (shp["func"] = "line") Then
      shp["wx"] = x
      shp["wy"] = y
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    If shp["func"] = "btn" Then
      shp["obj"] = Controls.AddButton(shp["caption"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  spr["shape"] = shape
  sprite[nSprite] = spr
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub

Sub Sprite_GetIndexOf
  ' Sprite | get index of a sprite
  ' param name - a sprite name
  ' return i - index of the sprite
  i = 0 ' not found
  For _i = 1 To nSprite
    spr = sprite[_i]
    If spr["name"] = name Then
      i = _i
      _i = nSprite ' exit For
    EndIf
  EndFor
EndSub

Sub Sprite_Rotate
  ' Sprite | rotate a sprite
  ' param sprite[i] - sprite to rotate
  ' param cx, cy - rotation center (if given)
  ' param angle - to rotate
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  Stack.PushValue("local", n)
  spr = sprite[i]
  shape = spr["shape"]
  moved = "False"
  If cx <> "" Then
    moved = "True"
  Else
    cx = ""   ' to avoid syntax error
  EndIf
  If cy <> "" Then
    moved = "True"
  Else
    cy = ""   ' to avoid syntax error
  EndIf
  s = spr["scale"]
  If moved Then
    param["cx"] = (cx - spr["x"]) / s
    param["cy"] = (cy - spr["y"]) / s
  Else
    param["cx"] = spr["width"] / 2
    param["cy"] = spr["height"] / 2
  EndIf
  param["scale"] = 1
  spr["angle"] = angle
  param["angle"] = spr["angle"]
  n = Array.GetItemCount(shape)
  Stack.PushValue("local", i)
  For i = 1 To n
    shp = shape[i]
    param["x"] = shp["x"]
    param["y"] = shp["y"]
    param["width"] = shp["width"]
    param["height"] = shp["height"]
    Shapes_CalcRotateZoomPos()
    shp["rx"] = x
    shp["ry"] = y
    alpha = shp["angle"] + spr["angle"]
    If sbd And (shp["func"] = "line") And (alpha <> 0) Then
      x1 = shp["x1"]
      y1 = shp["y1"]
      x2 = shp["x2"]
      y2 = shp["y2"]
      pw = shp["pw"]
      SB_LineWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _alpha = Math.GetRadians(alpha)
      SB_RotateWorkAround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    Shapes.Move(shp["obj"], spr["x"] + x * s, spr["y"] + y * s)
    Shapes.Rotate(shp["obj"], shp["angle"] + spr["angle"])
    shape[i] = shp
  EndFor
  i = Stack.PopValue("local")
  spr["shape"] = shape
  sprite[i] = spr
  n = Stack.PopValue("local")
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
EndSub
  