' Pictionary - Count
' Version 1.7.1
' Copyright Â© 2020 Nonki Takahashi.  The MIT License.
' Last update 2020-05-18

SB_Workaround()
Init()

' initialize chart shapes
Shapes_Init_Counter()
name = "Counter"
Sprite_Add()
iCounter = nSprite

' initialize caption shapes
Shapes_Init_Caption()
name = "Caption"
Sprite_Add()

GraphicsWindow.PenWidth = 0
GraphicsWindow.BrushColor = "SteelBlue"
n = Math.GetRandomNumber(5) + 4
size = 20
x = 100
y = 40
gap = (gw - size - x * 2) / (n - 1)
For i = 1 To n
  rect[i] = Shapes.AddRectangle(size, size)
  Shapes.Move(rect[i], gw + 10, y)
  Program.Delay(300)
  Shapes.Animate(rect[i], x, y, 600)
  x = x + gap
EndFor
Program.Delay(600)
even = 29
odd = 30
spr = sprite[iCounter]
shape = spr["shape"]
shX = spr["x"]
shY = spr["y"]
shp = shape[odd]
x = shX + shp["x"]
y1 = shY + shp["y"]
For c = 1 To n
  Program.Delay(200)
  CountUp()
EndFor

Sub CountUp
  If Math.Remainder(c, 2) = 0 Then
    up = even
    lo = odd
  Else
    up = odd
    lo = even
  EndIf
  shp = shape[up]
  Shapes.SetText(shp["obj"], c)
  For dy = 0 To shp["fs"] Step shp["fs"] / 6
    shp = shape[lo]
    y = y1 + shp["fs"] + dy
    Shapes.Move(shp["obj"], x, y)
    shp = shape[up]
    y = y1 + dy
    Shapes.Move(shp["obj"], x, y)
    Program.Delay(50)
  EndFor
EndSub

Sub Init
  GraphicsWindow.Title = "Pictionary"
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
EndSub

Sub SB_RotateWorkaround
  ' Small Basic | rotate workaround for Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param _alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(_alpha) - y1 * Math.Sin(_alpha) + ox
  y = x1 * Math.Sin(_alpha) + y1 * Math.Cos(_alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | workaround for Silverlight / SBD
  ' return silverlight - "True" if in remote
  ' return sbd - "True" if Small Basic Desktop
  color = GraphicsWindow.GetPixel(0, 0)
  sbd = "False"
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
    _gw = GraphicsWindow.Width
    _gh = GraphicsWindow.Height
    If (_gw = 624) And (_gh = 441) Then
      sbd = "True"
    EndIf
  EndIf
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | calculate total width and height of shapes
  ' param shape[] - shape array
  ' return shWidth, shHeight - total size of shapes
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Init_Caption
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 440 ' x offset
  shY = 358 ' y offset
  shape = ""
  shape[1] = "func=text;x=0;y=0;text=Count;fn=Trebuchet MS;fs=50;fb=True;pw=0;bc=Black;"
EndSub

Sub Shapes_Init_Counter
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 209 ' x offset
  shY = 100 ' y offset
  shape = ""
  shape[1] = "func=ell;x=0;y=40;width=40;height=40;pw=0;bc=Silver;"
  shape[2] = "func=ell;x=140;y=40;width=40;height=40;pw=0;bc=Silver;"
  shape[3] = "func=ell;x=140;y=200;width=40;height=40;pw=0;bc=Silver;"
  shape[4] = "func=ell;x=0;y=200;width=40;height=40;pw=0;bc=Silver;"
  shape[5] = "func=rect;x=20;y=40;width=140;height=200;pw=0;bc=Silver;"
  shape[6] = "func=rect;x=0;y=60;width=180;height=160;pw=0;bc=Silver;"
  shape[7] = "func=rect;x=75;y=60;width=30;height=10;pw=0;bc=#222222;"
  shape[8] = "func=ell;x=65;y=5;width=50;height=40;pw=0;bc=DarkGray;"
  shape[9] = "func=rect;x=65;y=25;width=50;height=5;pw=0;bc=DarkGray;"
  shape[10] = "func=rect;x=80;y=25;width=20;height=25;pw=0;bc=LightGray;"
  shape[11] = "func=ell;x=65;y=0;width=50;height=40;pw=0;bc=LightGray;"
  shape[12] = "func=rect;x=80;y=50;width=20;height=5;pw=0;bc=#BBBBBB;"
  shape[13] = "func=rect;x=80;y=55;width=20;height=10;pw=0;bc=DarkGray;"
  shape[14] = "func=rect;x=180;y=105;width=40;height=30;pw=0;bc=#222222;"
  shape[15] = "func=tri;x=198;y=85;x1=2;y1=0;x2=0;y2=40;x3=4;y3=40;angle=90;pw=0;bc=#222222;"
  shape[16] = "func=tri;x=198;y=115;x1=2;y1=0;x2=0;y2=40;x3=4;y3=40;angle=90;pw=0;bc=#222222;"
  shape[17] = "func=ell;x=10;y=80;width=20;height=20;pw=0;bc=#222222;"
  shape[18] = "func=ell;x=150;y=80;width=20;height=20;pw=0;bc=#222222;"
  shape[19] = "func=ell;x=150;y=140;width=20;height=20;pw=0;bc=#222222;"
  shape[20] = "func=ell;x=10;y=140;width=20;height=20;pw=0;bc=#222222;"
  shape[21] = "func=rect;x=20;y=80;width=140;height=80;pw=0;bc=#222222;"
  shape[22] = "func=rect;x=10;y=90;width=160;height=60;pw=0;bc=#222222;"
  shape[23] = "func=text;x=47;y=108;text=0;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[24] = "func=text;x=47;y=88;text=1;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[25] = "func=text;x=72;y=108;text=0;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[26] = "func=text;x=72;y=88;text=1;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[27] = "func=text;x=97;y=108;text=0;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[28] = "func=text;x=97;y=88;text=1;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[29] = "func=text;x=122;y=108;text=0;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[30] = "func=text;x=122;y=88;text=1;fn=Trebuchet MS;fs=20;fb=False;fi=False;pw=0;bc=White;"
  shape[31] = "func=rect;x=20;y=90;width=20;height=60;pw=0;bc=#111111;"
  shape[32] = "func=rect;x=140;y=90;width=20;height=60;pw=0;bc=#333333;"
  shape[33] = "func=tri;x=20;y=90;x1=20;y1=0;x2=0;y2=20;x3=40;y3=20;angle=180;pw=0;bc=Black;"
  shape[34] = "func=rect;x=40;y=90;width=100;height=20;angle=180;pw=0;bc=Black;"
  shape[35] = "func=tri;x=120;y=90;x1=20;y1=0;x2=0;y2=20;x3=40;y3=20;angle=180;pw=0;bc=Black;"
  shape[36] = "func=tri;x=20;y=130;x1=20;y1=0;x2=0;y2=20;x3=40;y3=20;pw=0;bc=#444444;"
  shape[37] = "func=rect;x=40;y=130;width=100;height=20;pw=0;bc=#444444;"
  shape[38] = "func=tri;x=120;y=130;x1=20;y1=0;x2=0;y2=20;x3=40;y3=20;pw=0;bc=#444444;"
EndSub

Sub Sprite_Add
  ' Sprite | add shapes to a sprite
  ' param name - sprite name
  ' param shX, shY - origin of shape array
  ' param scale - to resize 
  ' param shape[] - shape array
  ' param nSprite - number of sprite
  ' return nSprite - updated number of sprite
  ' return sprite[] - sprite array
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  nSprite = nSprite + 1
  spr = ""
  spr["name"] = name
  spr["x"] = shX
  spr["y"] = shY
  spr["angle"] = 0
  spr["dir"] = 1
  Shapes_CalcWidthAndHeight()
  spr["width"] = shWidth
  spr["height"] = shHeight
  spr["cx"] = shWidth / 2
  spr["cy"] = shHeight / 2
  If scale = "" Then
    scale = 1
  EndIf
  s = scale
  spr["scale"] = s
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text|btn", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If Text.IsSubText("text|btn", shp["func"]) Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      If shp["fb"] = "False" Then
        GraphicsWindow.FontBold = "False"
      Else
        GraphicsWindow.FontBold = "True"
      EndIf
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If sbd And (shp["func"] = "line") Then
      shp["wx"] = x
      shp["wy"] = y
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    If shp["func"] = "btn" Then
      shp["obj"] = Controls.AddButton(shp["caption"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  spr["shape"] = shape
  sprite[nSprite] = spr
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub
  